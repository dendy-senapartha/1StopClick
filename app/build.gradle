buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'io.fabric'

android {
    compileSdkVersion project.androidCompileSdkVersion
    defaultConfig {
        applicationId "com.a1stopclick"
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${buildNumber}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
            debuggable true
            ndk {
                abiFilters "armeabi-v7a","x86"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "armeabi-v7a"
            }
        }
    }
    applicationVariants.all { variant ->
        def appName
        if(project.hasProperty("applicationName"))
        {
            appName = applicationName
        } else {
            appName = parent.name
        }
        variant.outputs.all {
            def version = variant.versionName
            outputFileName = "${appName}-${version}.apk"
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    rootProject.appDependencies.each {
        add(it.configuration, it.dependency, it.options)
    }
    implementation project(path: ':vlcplayer')
    implementation project(path: ':domain')
    implementation project(path: ':data')
    implementation ('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true;
    }
}
